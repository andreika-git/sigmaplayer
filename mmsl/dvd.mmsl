
screen.back = ""
cancel_setup = 1
cancel_popup = 1
pad.clear = "all"
pad.set = "dvd"

// play dvd by default
set_dvd_source = 1
on player_source_set == 0
	on set_dvd_source == 1
		player.source = "dvd/"
		set_dvd_source = 0

player.language_menu = settings.dvd_lang_menu
player.language_audio = settings.dvd_lang_audio
player.language_subtitle = settings.dvd_lang_spu

screen.preload = font1
screen.preload = font2
screen.preload = "img/player/popup.gif"
screen.preload = "img/player/invalid.gif"
screen.preload = "img/player/subtitle.gif"
screen.preload = "img/player/subtitleoff.gif"
screen.preload = "img/player/audio.gif"
screen.preload = "img/player/angle.gif"

search_title = "DVD Search"
filesize_string = ""
filedate_string = ""

//player.debug = 1

search = 0

player.command = "play"

continue_cnt = 6
continue_msg = "Playback will continue at saved position\n                      in "

test_saved = 1

allow_zoom = 1
allow_osd = 1

// play?
on test_saved == 0
	on menu == 0
		on pad.key == "play"
			show_fast_balloon = "player/play"
			player.command = "play"
	on menu != 0
		on player.speed != 1
			on pad.key == "play"
				player.command = "play"
		on player.speed == 1
			on pad.key == "play"
				show_balloon = ""
				player.command = "press"

// stop?		
on pad.key == "stop"
	dvd = 0
	show_fast_balloon = "player/stop"
	auto_close_popup = 0
	show_popup_text = "The current disc position was saved!\nPress [Play] to resume playing.\nPress [Enter] to open file browser.\nPress [Setup] to open setup."

////////////////////////
// react on errors:
on player.speed != 0
	on player.error == "invalid"
		show_fast_balloon = "player/invalid"

///////////////////////
// process keys:

on pad.key == "forward"
	player.command = "forward"

on pad.key == "rewind"
	player.command = "rewind"

on pad.key == "prev"
	show_fast_balloon = "player/prev"
	player.command = "prev"

on pad.key == "next"
	show_fast_balloon = "player/next"
	player.command = "next"

on pad.key == "slow"
	player.command = "slow"		// auto-process slow fwd/rev

// pause pressed
on player.menu == 0
	allow_zoom = 1
	allow_osd = 1
	on pad.key == "pause"
		do_pause = 1
on player.menu == 1
	cancel.zoom = 1
	allow_zoom = 0
	allow_osd = 0
	on pad.key == "pause"
		show_fast_balloon = "player/invalid"

// -> pause
on player.speed != 0
	on do_pause == 1
		show_static_balloon = "player/pause"
		do_pause = 0
		player.command = "pause"
	
// already is paused mode -> step
on player.speed == 0
	on do_pause == 1
		restore_balloon = "player/pause"
		show_fast_balloon_and_restore = "player/stepfwd"
		do_pause = 0
		player.command = "step"

on search == 0 && zoom_mode == 0 && test_saved == 0
    on pad.key == "return"
    	show_fast_balloon = "player/return"
    	player.command = "return"

	on pad.key == "enter"
	    show_balloon = ""
	   	player.command = "press"

    on pad.key == "left"
    	player.command = "left"
    on pad.key == "right"
    	player.command = "right"
    on pad.key == "up"
    	player.command = "up"
    on pad.key == "down"
    	player.command = "down"

on search == 0
    on pad.key == "menu"
    	zoom_mode = 0
    	player.command = "menu"
	on pad.key == "title"
    	zoom_mode = 0
    	player.command = "rootmenu"
    on pad.key == "angle"
    	player.command = "angle"
    on pad.key == "audio"
    	player.command = "audio"
    on pad.key == "subtitle"
    	player.command = "subtitle"

/////////////////////////
on player.saved == 1
	on test_saved == 1
		kernel.print = "Disc position was saved. Continue playback?"
		player.command = "pause"
		popup_timer = 1000
		auto_close_popup = 0
		show_popup_text = "" + continue_msg + continue_cnt + " seconds"
		add image cancel
			cancel.group = "popup"
			cancel.halign = "center"
			cancel.valign = "center"
			cancel.src = "img/player/cancel.gif"
			cancel.x = popup.x
			cancel.y = popup.y + 55

		on continue_cnt <= 1
			on popuptext.timer == 0
				dvd_continue_play = 1

		on continue_cnt > 1
			on popuptext.timer == 0
				continue_cnt = continue_cnt - 1
				popuptext.value = "" + continue_msg + continue_cnt + " seconds"
				popuptext.timer = 1000

			on pad.key == "enter"
				continue_cnt = 0
				dvd_continue_play = 1

		on pad.key == "cancel"
			cancel_popup = 1
			kernel.print = "Cancel!"
			player.command = "play"
			test_saved = 0

		on dvd_continue_play == 1
			cancel_popup = 1
			kernel.print = "Continue!"
			player.command = "continue"
			test_saved = 0
on player.saved == 0
	on test_saved == 1
		test_saved = 0

/////////////////////////
// display speed:

on show_balloon != "player/play" && search != 1
	on player.speed == 0
		show_static_balloon = "player/pause"
	on player.speed == 1
		show_balloon = ""
on player.speed == 8
	show_static_balloon = "player/fwd8x"
on player.speed == 16
	show_static_balloon = "player/fwd16x"
on player.speed == 32
	show_static_balloon = "player/fwd32x"
on player.speed == 48
	show_static_balloon = "player/fwd48x"
on player.speed == -8
	show_static_balloon = "player/rev8x"
on player.speed == -16
	show_static_balloon = "player/rev16x"
on player.speed == -32
	show_static_balloon = "player/rev32x"
on player.speed == -48
	show_static_balloon = "player/rev48x"
on player.speed == "1/2"
	show_static_balloon = "player/slowfwd2x"
on player.speed == "1/4"
	show_static_balloon = "player/slowfwd4x"
on player.speed == "1/8"
	show_static_balloon = "player/slowfwd8x"


/////////////////////////////
// display subtitle/audio languages:

on player.menu == 0
	on player.subtitle_stream == 0
		show_fast_balloon = "player/subtitleoff"
	on player.subtitle_stream > 0
		show_fast_balloon = "player/subtitle"
		show_balloon_text = player.language_subtitle + " ("+ player.subtitle_stream + ")"
	on player.audio_stream > 0
		show_fast_balloon = "player/audio"
		show_balloon_text = player.language_audio + " ("+ player.audio_stream + ")"

	on player.angle > 0
		show_fast_balloon = "player/angle"
		show_balloon_text = "Camera " + player.angle

/////////////////////////////
// PAL/NTSC autodetect:

on settings.user2 == 1
	on player.frame_rate > 0
		autodetect_pal_ntsc = 1

